plugins {
	id 'com.github.johnrengelman.shadow' version '7.1.0'
	id 'fabric-loom' version '0.9-SNAPSHOT'
	id 'maven-publish'
	id 'java'
}

import net.fabricmc.loom.task.RemapJarTask

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

configurations {
	shade
}

repositories {
	maven {
		url "https://maven.terraformersmc.com/releases/"
		content {
			includeGroup "com.terraformersmc"
		}
	}
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:$minecraft_version"
	mappings "net.fabricmc:yarn:$yarn_mappings:v2"
	modImplementation "net.fabricmc:fabric-loader:$loader_version"

	modCompileOnly("com.terraformersmc:modmenu:$modmenu_version") {
		transitive(false)
	}

	shade implementation("org.python:jython:$jython_version")
}

shadowJar {
	dependsOn jar
	configurations = [project.configurations.shade]
	archiveClassifier.set('dev')
}
remapJar.dependsOn(shadowJar)

task shadowRemapJar(type: RemapJarTask) {
	dependsOn shadowJar

	input.set(file("${project.buildDir}/libs/$archivesBaseName-${project.version}-dev.jar"))
	archiveFileName = "${archivesBaseName}-${project.version}.jar"
	addNestedDependencies.set(true)
}
build.dependsOn(shadowRemapJar)

processResources {
	inputs.property 'version', project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	//noinspection GrDeprecatedAPIUsage
	classifier = 'sources'
	from sourceSets.main.allSource
}

jar {
	from('LICENSE') {
		rename {"${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		//noinspection GroovyAssignabilityCheck
		mavenJava(MavenPublication) { publication ->
			// add all the jars that should be included when publishing to maven
			//noinspection GroovyAssignabilityCheck
			artifact(remapJar) {
				//noinspection GroovyAssignabilityCheck
				builtBy remapJar
			}
			//noinspection GroovyAssignabilityCheck
			artifact(sourcesJar) {
				//noinspection GroovyAssignabilityCheck
				builtBy remapSourcesJar
			}
//			pom.withXml {
//				def depsNode  = asNode().appendNode('dependencies')
//
//				def depNode  = depsNode.appendNode('dependency')
//				depNode.appendNode('groupId', 'org.python')
//				depNode.appendNode('artifactId', 'jython')
//				depNode.appendNode('version', jython_version)
//				depNode.appendNode('scope', 'runtime')
//			}
		}
	}

	// publish to
	repositories {
		maven {
			credentials {
				username System.getenv('repo_username')
				password System.getenv('repo_password')
			}
			url = 'https://repo.repsy.io/mvn/enderzombi102/mc/'
		}
	}
}
